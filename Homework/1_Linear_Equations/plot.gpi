set terminal svg size 900,600 enhanced font 'Arial,16' background rgb "white"
set output 'QR_factorize_time.svg'

set title "QR-factorize an N×N matrix"
set xlabel "Matrix Size [N]"
set ylabel "Time [Seconds]"
set grid

set fit logfile 'QR_factorize_time_fit.log'

f(x) = a * x**3
fit f(x) 'out.times.data' using 1:2 via a

set key top left

set label sprintf("Fit: a = %.2e", a) at graph 0.05, 0.82 font "Arial,16" front

plot 'out.times.data' using 1:2 with points lt rgb "blue" pt 7 title "Measured Time", \
     f(x) with lines lt rgb "red" lw 2 title "Fit: a·N^3"
